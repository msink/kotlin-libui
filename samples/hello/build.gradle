apply plugin: 'konan'

def konanUserDir = System.getenv("KONAN_DATA_DIR") ?: "${System.getProperty("user.home")}/.konan"
def windresDir = "$konanUserDir/dependencies/msys2-mingw-w64-x86_64-gcc-7.3.0-clang-llvm-lld-6.0.1/bin"
def rcFile = file('src/main/resources/samples.rc')
def resFile = file("$buildDir/konan/resources/samples.res")
task windowsResources(type: Exec) {
    inputs.file rcFile
    outputs.file resFile
    commandLine "$windresDir/windres", rcFile, '-O', 'coff', '-o', resFile
    environment 'PATH', "$windresDir;${System.getenv('PATH')}"
}

konanArtifacts {
    program('hello') {
        libraries {
            artifact project(':libui-ktx'), 'libui-ktx'
        }
        target('mingw') {
            dependsOn 'windowsResources'
            inputs.file resFile
            linkerOpts "$resFile -mwindows"
        }
    }
}
