plugins {
    id 'de.undercouch.download' version '3.4.3'
    id 'kotlin-multiplatform' version '1.3.0'
}

repositories {
    jcenter()
    mavenCentral()
}

final def os = org.gradle.internal.os.OperatingSystem.current()

task downloadArchive(type: Download) {
    def release = "https://github.com/andlabs/libui/releases/download/$libuiVersion/libui-$libuiVersion"
    if (os.isWindows()) {
        src "$release-windows-amd64-mingw-static.zip"
    } else if (os.isLinux()) {
        src "$release-linux-amd64-static.tgz"
    } else if (os.isMacOsX()) {
        src "$release-darwin-amd64-static.tgz"
    }
    dest new File(buildDir, "libui-$libuiVersion.${os.isWindows() ? "zip" : "tgz"}")
    overwrite false
}

task unpackArchive(dependsOn: downloadArchive, type: Copy) {
    if (os.isWindows()) {
        from zipTree(downloadArchive.dest)
    } else {
        from tarTree(resources.gzip(downloadArchive.dest))
    }
    into buildDir
}

kotlin {
    targets {
        fromPreset(presets.macosX64, 'macosx')
        fromPreset(presets.linuxX64, 'linux')
        fromPreset(presets.mingwX64, 'windows')

        configure([macosx, linux, windows]) {
            compilations.main {
                cinterops {
                    libui {
                        includeDirs "${project.rootDir}/libui/build"
                    }
                }
            }
        }
    }

    sourceSets {
        nativeMain {
            kotlin.srcDir('src/nativeMain/kotlin')
        }
        macosxMain {
            dependsOn nativeMain
        }
        linuxMain {
            dependsOn nativeMain
        }
        windowsMain {
            dependsOn nativeMain
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.CInteropProcess).all {
    dependsOn unpackArchive
}
